apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'koin'

repositories {
    mavenCentral()
}

configurations {
    ktlint
}

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "kaleidot725.exifreader"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 2
        versionName "1.1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    ktlint "com.pinterest:ktlint:0.35.0"
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'

    implementation "org.koin:koin-android:2.2.2"
    implementation "org.koin:koin-android-scope:2.2.2"
    implementation "org.koin:koin-android-viewmodel:2.2.2"

    implementation 'pub.devrel:easypermissions:3.0.0'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.3'

    implementation "io.coil-kt:coil:0.8.0"

    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "com.mikepenz:aboutlibraries:6.1.1"

    implementation "com.github.hadilq.liveevent:liveevent:1.2.0"

    implementation "androidx.exifinterface:exifinterface:1.3.2"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}